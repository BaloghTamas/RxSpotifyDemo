apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "hu.bme.aut.android.spotifydemo"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        versionName "1.2"
        testInstrumentationRunner "hu.bme.aut.android.spotifydemo.utils.KeyguardTestRunner"
    }

    signingConfigs {
        debug {
            storeFile file("../RxSpotifyDemo_debug.keystore")
        }
        release {
            storeFile file("../RxSpotifyDemo.keystore")
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias "RxSpotifyDemo"
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    /*
    MD5:  54:DC:D6:36:93:61:49:33:01:A9:EC:FC:EC:46:2A:81
    SHA1: D7:A1:7D:45:51:A1:99:15:52:FB:D1:78:5A:7F:EB:77:B7:3D:5B:23
    SHA256: 4A:0F:A9:7C:5D:52:C4:D9:FF:3D:D4:64:62:4D:B4:15:E4:39:01:11:59:27:39:F9:89:32:1A:EE:77:68:B7:F0
    SHA1-BASE64: 16F9RVGhmRVS+9F4Wn/rd7c9WyM=
    */
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}




dependencies {
    implementation 'com.google.code.gson:gson:2.8.3'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Dagger 2 and Compiler
    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor "com.google.dagger:dagger-compiler:2.15"
    runtimeOnly 'javax.annotation:jsr250-api:1.0'

    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'

    //JVM Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.7.1'
    testImplementation "org.mockito:mockito-core:2.15.0"

    testImplementation 'com.google.dagger:dagger:2.15'
    testAnnotationProcessor "com.google.dagger:dagger-compiler:2.15"
    testRuntimeOnly 'javax.annotation:jsr250-api:1.0'

    //Android Test
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2-alpha1')
            {
                exclude group: 'com.google.code.findbugs', module: 'jsr305'
            }
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation "org.mockito:mockito-core:2.15.0"

    androidTestImplementation 'com.google.dagger:dagger:2.15'
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:2.15"
    androidTestRuntimeOnly 'javax.annotation:jsr250-api:1.0'

}
